plugins {
    alias libs.plugins.android.application apply false
    alias libs.plugins.android.library apply false
    alias libs.plugins.kotlin.android apply false
    alias libs.plugins.kotlin.jvm apply false
    alias libs.plugins.ksp apply false
    alias libs.plugins.hilt apply false
    alias libs.plugins.gradle.versions apply true
    alias libs.plugins.version.catalog.update apply true
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

tasks.named("dependencyUpdates").configure {
    def isNonStable = { String version ->
        def nonStableKeyword = ['dev', 'alpha', 'beta', 'rc', 'm'].any {
            qualifier -> version.toLowerCase().contains(qualifier)
        }
        return nonStableKeyword
    }
    def isStable = { String version ->
        return !isNonStable(version)
    }
    rejectVersionIf {
        isNonStable(it.candidate.version) && isStable(it.currentVersion)
    }
}